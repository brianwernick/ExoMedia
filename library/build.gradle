apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'bintray-release'

def versionMajor = 3
def versionMinor = 0
def versionPatch = 3

def libraryGroupId = 'com.devbrackets.android'
def libraryBaseName = 'exomedia'
def libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}" // release version

dependencies {
    // Android
    compile 'com.android.support:appcompat-v7:24.2.1' // includes support-v4

    // ExoPlayer
    compile 'com.google.android.exoplayer:exoplayer:r1.5.11'

    //Android unit tests
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "junit:junit:4.12"
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// ======== MAVEN DEPLOY to local cache or nexus repo (to make accessible to local projects) (command: ./gradlew clean aRel uploadArchives) ========
uploadArchives {
    repositories {
        // deploy to local
        mavenDeployer {
            repository (url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = libraryGroupId
            pom.artifactId = libraryBaseName
            pom.version = libraryVersion //Normally I would append "-SNAPSHOT" but newer gradle/maven can't always find them
            pom.packaging = 'aar'
            uniqueVersion = false // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// ======= BINTRAY DEPLOY (command: $ ./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=false) ====
publish {
    groupId = libraryGroupId
    artifactId = libraryBaseName
    uploadName = 'ExoMedia'
    version = libraryVersion
    autoPublish = false
    dryRun = false
}
