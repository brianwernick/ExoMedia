buildscript {
  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath libs.android.gradle
    classpath libs.kotlin.gradle
  }
}

plugins {
  alias libs.plugins.android.application apply false
  alias libs.plugins.android.library apply false
  alias libs.plugins.compose.compiler apply false
  alias libs.plugins.kotlin.android apply false
  alias libs.plugins.nexus.publish
}

apply from: './gradle/util/testOutput.gradle'

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

/*
 * Below is the functionality to publish the `library` module to Maven Central (Sonatype) using the
 * plugin `io.github.gradle-nexus.publish-plugin`. This functionality is included in this (root)
 * `build.gradle` because that's what the plugin expects; if this changes in the future then we
 * should migrate this functionality into the `publish.gradle` file instead.
 */
apply from: 'gradle/release/libraryInfo.gradle'

ext {
  libraryInfo = getLibraryInfo()
}

// Used by the publish-plugin
group = libraryInfo.groupId
version = libraryInfo.versionName

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      username = System.getenv("SONATYPE_TOKEN_USERNAME")
      password = System.getenv("SONATYPE_TOKEN_PASSWORD")
    }
  }
}